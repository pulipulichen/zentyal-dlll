<%doc>
  This template will show the content from a model composite, that is
  the content from its components in an established layout.

  Parameters:
    model - <EBox::Model::Composite> the composite model to show
    hasChanged - Boolean indicating whether the composite model has
    changed or not
</%doc>
<%args>
  $model
  $hasChanged
</%args>
<%init>
  use EBox::Gettext;
  use Data::Dumper;
</%init>
<& .pageTitle,
   crumbs => $model->HTMLTitle()
&>
<%perl>
  # Annotate we are in composite after printing own title
  $m->notes('composite', 1);
</%perl>
<& .headTitle,
   title => $model->headTitle()
&>
% if ($model->precondition()) {
%    # Define the components when we are sure the precondition is
%    # matched
%    my $componentsRef = $model->components();
%    # Print the help if any
%    my $help = $model->help();
%    if ($help) {
<div class="help">
  <% $model->help() %>
</div>
%    }
%    # Print the disabled module warning if needed
%    my $disabledModuleWarning = $model->disabledModuleWarning();
%    if ($disabledModuleWarning) {
<div class="warning">
  <% $disabledModuleWarning %>
</div>
%    }
%    # Print the permanent message if any
%    my $permanentMessage = $model->permanentMessage();
%    my $permanentMessageType = $model->permanentMessageType();
%    if ($permanentMessage) {
<div class="<% $permanentMessageType %>">
  <% $permanentMessage %>
</div>
%    }
%    if ( $model->layout() eq 'tabbed' )
%    {
<& /ajax/tabMenu.mas,
   models => $componentsRef,
   selectedTab => 0,
   hasChanged => $hasChanged,
   tabName    => $model->name(),
   directory  => $model->directory(),
&>
%    }
%    elsif ( $model->layout() eq 'top-bottom' )
%    {
%       foreach my $component (@{$componentsRef}) {
<& $component->Viewer(),
   model => $component,
   hasChanged => $hasChanged,
   action => 'view',
&>
%       }
%    }
%    elsif ( $model->layout() eq 'select' )
%    {
<& /ajax/selector.mas,
   composite => $model,
   hasChanged => $hasChanged,
&>
%    }
% }
% else
% {
%   my $failMsg =  $model->preconditionFailMsg();
%   if ($failMsg) {
<div class="note">
  <% $failMsg %>
</div>
%   }
% }
<%perl>
  # Annotate we are no longer in composite
  $m->notes('composite', 0);
</%perl>

<%doc>
  Method: pageTitle

     Set the page title

  Parameters:

     title - String the title

</%doc>
<%def .pageTitle>
<%args>
  $crumbs => undef
</%args>
<& /title.mas,
    crumbs => $crumbs
&>
</%def>

<%doc>
Method: .headTitle

   Private component to print the title if any. It takes into account
   if the composite is the root one or not to determine which kind of
   title it is

Parameters:

   title - String the title to print

</%doc>
<%def .headTitle>
<%args>
  $title => undef
</%args>

% if ($title) {
<h3>
  <% $title %>
</h3>
% }

</%def>

